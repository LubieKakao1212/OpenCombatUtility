buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}

plugins {
    id "com.github.johnrengelman.shadow" version "1.2.3"
}

apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.


version = "1.0"
group = "com.LubieKakao1212.opencu" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "OpenCU"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20171003"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
	maven {
		name = "CoFH Maven"
		url = "https://maven.covers1624.net"
	}
	maven {
		name = "ChickenBones Maven"
		url = "http://chickenbones.net/maven/"
	}
	maven {
		name = "JEI Maven"
		url = "https://dvs1.progwml6.com/files/maven"
	}
	maven { 
		url = "http://maven.cil.li/" 
	}
    maven {
        url "https://jitpack.io"
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
    compile "org.joml:joml-jdk8:${jomlVersion}"
    compile "li.cil.oc:OpenComputers:MC1.12.2-1.7.5.+"
	compile "cofh:RedstoneFlux:1.12-2.1.0.+:universal"
    compile "cofh:CoFHCore:1.12.2-4.6.0.+:universal"
    compile "cofh:CoFHWorld:1.12.2-1.3.0.+:universal"
    compile "cofh:ThermalFoundation:1.12.2-2.6.0.+:universal"
    compile "cofh:ThermalExpansion:1.12.2-5.5.4.+:universal"

    compile "codechicken:CodeChickenLib:1.12.2-3.1.7.+:universal"
	compile "mezz.jei:jei_1.12.2:4.8.0.+"
}

shadowJar {
    classifier = ""
    dependencies {
        include("org.joml:joml-jdk8:${jomlVersion}")
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

tasks.build.dependsOn shadowJar
artifacts {
    archives shadowJar
}

reobf {
    shadowJar {
        mappingType = "SEARGE"
    }
}
tasks.reobfShadowJar.mustRunAfter shadowJar

tasks.build.dependsOn reobfShadowJar